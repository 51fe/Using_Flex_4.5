<?xml version="1.0"?>
<!-- usingas/IntrospectionAPI.mxml -->
<s:Application 
    xmlns:fx="http://ns.adobe.com/mxml/2009"    
    xmlns:mx="library://ns.adobe.com/flex/mx"     
    xmlns:s="library://ns.adobe.com/flex/spark"
    creationComplete="getDetails()">

    <s:layout> 
        <s:VerticalLayout/> 
    </s:layout>
    
    <fx:Script>
        <![CDATA[
        import flash.utils.*;

        public function getDetails():void { 
            // Get the Button control's E4X XML object description.
            var classInfo:XML = describeType(button1);

            // Dump the entire E4X XML object into ta2.
            ta2.text = classInfo.toString();

            // List the class name.
            ta1.text = "Class " + classInfo.@name.toString() + "\n";

            // List the object's variables, their values, and their types.
            for each (var v:XML in classInfo..variable) {
                ta1.text += "Variable " + v.@name + "=" + button1[v.@name] + 
                    " (" + v.@type + ")\n";
            }

            // List accessors as properties.
            for each (var a:XML in classInfo..accessor) {
                // Do not get the property value if it is write only.
                if (a.@access == 'writeonly') {
                    ta1.text += "Property " + a.@name + " (" + a.@type +")\n";
                }
                else {
                    ta1.text += "Property " + a.@name + "=" + 
                        button1[a.@name] +  " (" + a.@type +")\n";
                }
            } 

            // List the object's methods.
            for each (var m:XML in classInfo..method) {
                ta1.text += "Method " + m.@name + "():" + m.@returnType + "\n";
            }
        }
        ]]>
    </fx:Script>

    <s:Button label="This Button Does Nothing" id="button1"/>
    <s:TextArea id="ta1" width="400" height="200"/>
    <s:TextArea id="ta2" width="400" height="200"/>
</s:Application>