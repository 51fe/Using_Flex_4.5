<?xml version="1.0"?>
<!-- modules/QueryStringModule.mxml -->
<s:Module 
    xmlns:fx="http://ns.adobe.com/mxml/2009" 
    xmlns:s="library://ns.adobe.com/flex/spark" 
    xmlns:mx="library://ns.adobe.com/flex/mx"
    creationComplete="parseString()">

    <s:layout> 
        <s:VerticalLayout/> 
    </s:layout>

    <fx:Script>
        <![CDATA[
            import mx.utils.*;

            [Bindable]
            private var salutation:String;
            
            public var o:Object = {};      
            
            public function parseString():void {
                try {
                    /* Remove everything before the question mark, including
                       the question mark. */
                    var myPattern:RegExp = /.*\?/;  
                    var s:String = this.loaderInfo.url.toString();
                    s = s.replace(myPattern, "");

                    /* Create an Array of name=value Strings. */
                    var params:Array = s.split("&");
                    
                    /* Print the params that are in the Array. */
                    var keyStr:String;
                    var valueStr:String;
                    var paramObj:Object = params;
                    for (keyStr in paramObj) {
                        valueStr = String(paramObj[keyStr]);
                        ta1.text += keyStr + ":" + valueStr + "\n";
                    }

                    /* Set the values of the salutation. */
                    for (var i:int = 0; i < params.length; i++) {
                        var tempA:Array = params[i].split("=");                        
                        if (tempA[0] == "firstName") {
                            o.firstName = tempA[1];
                        } 
                        if (tempA[0] == "lastName") {
                            o.lastName = tempA[1];
                        } 
                    }                    
                    if (StringUtil.trim(o.firstName) != "" && 
                        StringUtil.trim(o.lastName) != "") {
                            salutation = "Welcome " + 
                            o.firstName + " " + o.lastName + "!";                           
                    } else {
                        salutation = "Full name not entered."
                    }
                } catch (e:Error) {
                    trace(e);
                }

                /* Show some of the information available through loaderInfo: */
               ta2.text = "AS version: " + this.loaderInfo.actionScriptVersion;
               ta2.text += "\nApp height: " + this.loaderInfo.height;
               ta2.text += "\nApp width: " + this.loaderInfo.width;
               ta2.text += "\nApp bytes: " + this.loaderInfo.bytesTotal;
            }
        ]]>
    </fx:Script>
    <s:Label text="{salutation}"/>
    <s:TextArea height="75" width="250" id="ta1"/>    
    <s:TextArea height="200" width="250" id="ta2"/>    
</s:Module>